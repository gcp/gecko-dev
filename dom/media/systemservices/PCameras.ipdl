/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PContent;
include protocol PBackground;

namespace mozilla {
namespace camera {

struct Camera
{
};

struct CaptureCapability
{
  int width;
  int height;
  int maxFPS;
  int expectedCaptureDelay;
  int rawType;
  int codecType;
  bool interlaced;
};

sync protocol PCameras
{
  manager PContent or PBackground;

child:
  CameraList(Camera[] args);
  FrameSizeChange(int w, int h);
  // transfers ownership of |buffer| from parent to child
  DeliverFrame(Shmem buffer, int size, uint32_t time_stamp, int64_t render_time);

parent:
  EnumerateCameras();
  sync NumberOfCaptureDevices() returns (int numdev);
  sync NumberOfCapabilities(nsCString deviceUniqueIdUTF8) returns (int numcap);
  sync GetCaptureCapability(nsCString unique_idUTF8, int capability_number)
                            returns (CaptureCapability capability);
  sync GetCaptureDevice(int num) returns (nsCString device_name, nsCString device_id);

  sync AllocateCaptureDevice(nsCString unique_idUTF8) returns (int numdev);
  sync ReleaseCaptureDevice(int numdev);
  sync StartCapture(int numdev, CaptureCapability capability);
  sync StopCapture(int numdev);
  // transfers frame back
  async ReleaseFrame(Shmem s);

  __delete__();
};

} // namespace camera
} // namespace mozilla
